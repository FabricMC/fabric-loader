apply plugin: "fabric-loom"

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

loom {
	runConfigs.configureEach {
		ideConfigGenerated = true
	}
}

dependencies {
	minecraft "com.mojang:minecraft:1.18"
	mappings "net.fabricmc:yarn:1.18+build.1:v2"

	implementation project(":minecraft")

	// Required for mixin annotation processor
	annotationProcessor "org.ow2.asm:asm:${project.asm_version}"
	annotationProcessor "org.ow2.asm:asm-analysis:${project.asm_version}"
	annotationProcessor "org.ow2.asm:asm-commons:${project.asm_version}"
	annotationProcessor "org.ow2.asm:asm-tree:${project.asm_version}"
	annotationProcessor "org.ow2.asm:asm-util:${project.asm_version}"

	/**
	 * Ensure we are using the mixin version loader is built against to test the AP.
	 * Otherwise Loom will default to an older version (due to no mod loader on the mod* configs)
	 */
	annotationProcessor ("net.fabricmc:sponge-mixin:${project.mixin_version}") {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 16
}